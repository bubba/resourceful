\chapter{Mechanisation}\label{cha:mechanisation}
In this chapter, we will look at how the proofs in
Chapter~\ref{cha:properties} were mechanically formalised --- in other
words, proved within a proof assistant.  I decided to mechanise the
proofs within Agda~\cite{norell2009}, a dependently typed programming
language with an ML syntax, similar to that of Haskell's. It can be
used for general purpose programming, but because it is rooted in
Martin-Löf intuitionistic type theory~\cite{martin-lof1984}, it can
also be used as a proof assistant.

Like many other proof assistants, the way we prove properties within
Agda is by writing programs that satisfy types. It takes advantage of
the Curry-Howard correspondence, which says that propositions are
analogous to types, and proofs are analogous to programs that fulfil
that type. We first create types that represent our
propositions. Then we can then prove our propositions by constructing
a value for the program that satisfies the type, hence intuitionistic
logic is also known as constructive logic.

As a brief example, a proposition of the form
$\forall a. a \rightarrow b$, translates into a program of type \mintinline{agda}{∀ a → b}. If you read the proposition as ``\textit{if a, then b}'', then
you can read the type as ``\textit{given any proof of a, then I can
  construct a proof of b}''. And that is indeed what the program
should do: it is a function that takes an argument of $a$, and returns
something of type $b$.

The vast majority of the work in mechanising was in writing the proofs
related to polymorphism in the Hindley-Damas-Milner system, not the
resourceful parts. The contemporary syntactic version of
Hindley-Damas-Milner (what we are using here) has been successfully
formalised in Coq~\cite{dubois2000}, and a formalisation for System F
was recently created in Agda~\cite{chapman2019}. However as it stands,
we are not aware of any formalizations of Hindley-Damas-Milner within
Agda. Regardless, the techniques and approaches here are similar, and
we base much of the proofs off of the proofs in~\cite{wright1994}.

\section{Definitions}
The framework within Agda for working with the type system is built
upon the formalisation of the simply typed lambda calculus by Kokke et
al.~\cite{kokke2020} It begins with the grammar, shown in
Listing~\ref{lst:grammar}.

\setminted{fontsize=\small,samepage}

\begin{listing}
\begin{multicols}{2}
\begin{minted}[fontsize=\small,samepage]{agda}
data Term : Set where
  `_        : Id → Term
  ƛ_⇒_      : Id → Term → Term
  _·_       : Term → Term → Term
  lt_⇐_in'_ : Id → Term → Term → Term
  ⟦_⟧       : Term → Term
  _>>=_     : Term → Term → Term
  □         : Term
  use       : Resource → Term → Term
  _×_       : Term → Term → Term
  π₁        : Term → Term
  π₂        : Term → Term
  _⋎_       : Term → Term → Term
\end{minted}
\begin{minted}[fontsize=\small,samepage]{agda}
data Heap : Set where
  World : Heap
  `_    : Resource → Heap
  _∪_   : Heap → Heap → Heap
\end{minted}
\begin{minted}[fontsize=\small]{agda}
data Type : Set where
  `_  : Id → Type
  _⇒_ : Type → Type → Type
  IO  : Heap → Type → Type
  □   : Type
  _×_ : Type → Type → Type
\end{minted}
\begin{minted}[fontsize=\small]{agda}
data TypeScheme : Set where
  V_·_ : Id → TypeScheme → TypeScheme
  `_   : Type → TypeScheme
\end{minted}
\end{multicols}
\caption{Grammar definitions. Note that some of the notation (e.g.~$\forall$) had to be substituted due to
being reserved within Agda.}\label{lst:grammar}
\end{listing}

For relations, such as the heap well-formed relation
$\textsf{ok} \ \rho$, we define a new data type. The well-formed relation
is a relation on a heap, and so the data type is parameterised over
it. Furthermore, the rules directly correspond to data constructors ---
constructing one of these means we have constructed a proof that the
heap is well typed.
\begin{minted}{agda}
data Ok : Heap → Set where
  OkZ : ∀ {r}
        --------
      → Ok (` r)
  OkS : ∀ {a b}
       → Ok a
       → Ok b
       → a ∩ b =∅
         ----------
       → Ok (a ∪ b)
  OkWorld : --------
            Ok World
\end{minted}
As another example, we also define a data type to represent proof that
a heap is a subheap of another heap. This data type is parameterised
over two heaps, which appear in the type.
\begin{minted}[samepage]{agda}
data _≥:_ : Heap → Heap → Set where
  ≥:World : ∀ {ρ} → ρ ≥: World
  ≥:Refl : ∀ {ρ} → ρ ≥: ρ
  ≥:∪ˡ : ∀ {ρ ρ' ρ''}
       → ρ ≥: ρ'
         ------------
       → ρ ≥: ρ' ∪ ρ''
  ≥:∪ʳ : ∀ {ρ ρ' ρ''}
       → ρ ≥: ρ'
         ------------
       → ρ ≥: ρ'' ∪ ρ'
\end{minted}
If we wanted to show that $\textsf{Net} \subtyp \textsf{Net} \cup
\textsf{File}$, we follow the same steps we would carry out in a proof
tree in order to construct a value that inhabits the type \mintinline{agda}{` Net ≥: ` Net ∪ ` File}.
\begin{listing}[H]
  \centering
  \begin{minipage}{0.5\linewidth}
    \begin{minted}{agda}
_ : ` Net ≥: ` Net ∪ ` File
_ = ≥:∪ʳ ≥:Refl
    \end{minted}
  \end{minipage}%
  \begin{minipage}{0.5\linewidth}
    \[
      \infer*[Left=UnionL]{
        \infer*[Left=Refl]{ }{\textsf{\textsf{Net} \subtyp \textsf{Net}}}
      }
      {\textsf{Net} \subtyp \textsf{Net} \cup \textsf{File}}
    \]
  \end{minipage}
\end{listing}
The most important relation however, is the typing relation. With
Agda's Unicode support we are able to define the rules, shown in
Listing~\ref{lst:typingrules}, with a notation similar to what we used
in Chapter~\ref{chapter:system}. Typing judgements can be can
constructed as follows:
\begin{listing}[H]
  \centering
  \begin{minipage}{0.6\linewidth}
    \begin{minted}{agda}
_ : ∅ ⊢ ƛ "x" ⇒ ` "x" ⦂ (` "α" ⇒ ` "α")
_ = ⊢ƛ (⊢` Z (Inst SZ refl refl))
    \end{minted}
  \end{minipage}%
  \begin{minipage}{0.4\linewidth}
    \[
      \infer*[Left=Abs]{
        \infer*[Left=Var]{
          x : \alpha \in \centerdot, x : \alpha \\
          \alpha > \alpha
        }{
          \centerdot , x : \alpha \vdash x : \alpha
        }
      }
      {\centerdot \vdash \lambda x . x : \alpha \rightarrow \alpha}
    \]
  \end{minipage}
\end{listing}
The small-step relation is defined in a similar way:
\begin{minted}{agda}
data _↝_ : Term → Term → Set where
  ξ-·₁ : ∀ {e₁ e₂ e₁'}
       → e₁ ↝ e₁'
         ------------------
       → e₁ · e₂ ↝ e₁' · e₂
       
  ξ-·₂ : ∀ {e₁ e₂ e₂'}
       → e₂ ↝ e₂'
         ------------------
       → e₁ · e₂ ↝ e₁ · e₂'

  β-ƛ : ∀ {x e e'}
      → Value e'
        ------------------
      → (ƛ x ⇒ e) · e' ↝ e [ x := e' ]
  -- and so on ...
\end{minted}


\begin{listing}
  \begin{multicols}{2}
\begin{minted}[breaklines,samepage]{agda}
data _⊢_⦂_ : Context → Term → Type → Set where

  ⊢` : ∀ {Γ x σ τ}
     → x ⦂ σ ∈ Γ
     → σ > τ
       ---------
     → Γ ⊢ ` x ⦂ τ

  ⊢ƛ : ∀ {Γ x τ' τ e}
     → Γ , x ⦂ ` τ' ⊢ e ⦂ τ
       ------------------
     → Γ ⊢ ƛ x ⇒ e ⦂ (τ' ⇒ τ)

  ⊢· : ∀ {Γ e e' τ τ'}
     → Γ ⊢ e ⦂ τ' ⇒ τ
     → Γ ⊢ e' ⦂ τ'
       --------------
     → Γ ⊢ e · e' ⦂ τ

  ⊢lt : ∀ {Γ e e' τ τ' x}
      → Γ ⊢ e' ⦂ τ'
      → Γ , x ⦂ close Γ τ' ⊢ e ⦂ τ
        ---------------------
      → Γ ⊢ lt x ⇐ e' in' e ⦂ τ

  ⊢× : ∀ {Γ e e' τ τ'}
     → Γ ⊢ e ⦂ τ
     → Γ ⊢ e' ⦂ τ'
       --------------------
     → Γ ⊢ e × e' ⦂ τ × τ'

  ⊢π₁ : ∀ {Γ e τ τ'}
      → Γ ⊢ e ⦂ τ × τ'
        --------------
      → Γ ⊢ π₁ e ⦂ τ

  ⊢π₂ : ∀ {Γ e τ τ'}
      → Γ ⊢ e ⦂ τ × τ'
        --------------
      → Γ ⊢ π₂ e ⦂ τ'
\end{minted}
\begin{minted}[breaklines,samepage]{agda}
  ⊢□ : ∀ {Γ}
       
       ---------
     → Γ ⊢ □ ⦂ □

  -- Monadic rules

  ⊢⟦⟧ : ∀ {Γ e τ ρ}
      → Γ ⊢ e ⦂ τ
      → Ok ρ
        ----------------
      → Γ ⊢ ⟦ e ⟧ ⦂ IO ρ τ
      
  ⊢use : ∀ {Γ e τ r}
       → Γ ⊢ e ⦂ τ
         ---------------
       → Γ ⊢ use r e ⦂ IO (` r) τ
  
  ⊢>>= : ∀ {Γ e e' τ τ' ρ}
       → Γ ⊢ e ⦂ (IO ρ τ')
       → Γ ⊢ e' ⦂ (τ' ⇒ IO ρ τ)
         -------------------
       → Γ ⊢ e >>= e' ⦂ IO ρ τ

  ⊢⋎ : ∀ {Γ e₁ e₂ τ₁ τ₂ ρ₁ ρ₂}
     → Γ ⊢ e₁ ⦂ IO ρ₁ τ₁
     → Γ ⊢ e₂ ⦂ IO ρ₂ τ₂
     → Ok (ρ₁ ∪ ρ₂)
       -----------------------
     → Γ ⊢ e₁ ⋎ e₂ ⦂ IO (ρ₁ ∪ ρ₂) (τ₁ × τ₂)

  ⊢sub : ∀ {Γ e τ ρ ρ'}
         → Γ ⊢ e ⦂ IO ρ τ
         → ρ ≥: ρ'
         → Ok ρ'
           --------------
         → Γ ⊢ e ⦂ IO ρ' τ
\end{minted}
\end{multicols}
\caption{The typing rules as they are defined in Agda.}\label{lst:typingrules}
\end{listing}
\section{Type schemes and type variables}
One of the main design decisions made early on was how to represent
type schemes within Agda. Quantified type variables in type schemes are often
represented as a sequence
$\forall \alpha_1,\ldots,\alpha_n \cdot \tau$. This expands out to
$\forall \alpha_1 \cdot \cdots \cdot \forall \alpha_n \cdot \tau$ in the end, and is how type schemes are
ultimately defined in Agda, but we need to be able to reason about it
in sequence format for some of the proofs. A helper function
\texttt{VV} was created for this reason. It allows for type schemes
to be created and manipulated in terms of lists.
\begin{minted}{agda}
VV : List Id → Type → TypeScheme
VV (α ∷ αs) τ = V α · (VV αs τ)
VV [] τ = ` τ
\end{minted}
Now we can rewrite propositions, such as the substitution lemma
(Lemma~\ref{lem:substitution}), in a way that lets us bind the list
of quantified type variables and use it elsewhere.
\begin{minted}{agda}
subst : ∀ {Γ x e e' αs τ τ'}
      → Γ ⊢ e ⦂ τ
      → Γ , x ⦂ VV αs τ ⊢ e' ⦂ τ'
      → Disjoint αs (FTVC Γ)
        ----------------------
      → Γ ⊢ e' [ x := e ] ⦂ τ'
\end{minted}
There are also functions to extract the quantified type variables and
type from a type scheme, and equivalence proofs that can be used to
convince the type checker that they are equivalent to their
\texttt{VV} form.
\begin{minted}{agda}
TStype : TypeScheme → Type
TStype (V _ · σ) = TStype σ
TStype (` τ) = τ

TSvars : TypeScheme → List Id
TSvars (V α · σ) = α ∷ TSvars σ
TSvars (` τ) = []

TStype≡ : ∀ {αs τ} → TStype (VV αs τ) ≡ τ
TStype≡ {[]} {τ} = refl
TStype≡ {α ∷ αs} {τ} = TStype≡ {αs}

TSvars≡ : ∀ {αs τ} → TSvars (VV αs τ) ≡ αs
TSvars≡ {[]} = refl
TSvars≡ {α ∷ αs} = cong (_∷_ α) TSvars≡
\end{minted}

\section{Properties}
The type preservation theorem (Theorem~\ref{thm:preservation}) is proven
with the following function --- given proof that $e$ has type $\tau$ in the
empty context, and proof that $e$ reduces to $e'$ in one step, then we
can provide proof that $e'$ also has the type $\tau$ in the empty context.
\begin{minted}{agda}
preservation : ∀ {e e' τ}
             → ∅ ⊢ e ⦂ τ
             → e ↝ e'
               ----------
             → ∅ ⊢ e' ⦂ τ
\end{minted}
For progress (Theorem~\ref{thm:progress}), we need to be able to say
either the expression is a value or it reduces to something else. We
create a new data type to represent the two cases where this can
happen.
\begin{minted}{agda}
data Progress (e : Term) : Set where
  step : ∀ {e'}
       → e ↝ e'
         ------
       → Progress e
  done : Value e
         ----------
       → Progress e

progress : ∀ {e τ}
         → ∅ ⊢ e ⦂ τ
           ----------
         → Progress e
\end{minted}

\section{Postulations}
There are number of properties that are postulated throughout, and
these should eventually be proven at some point. Postulations do not
require any proofs and are just assumed by Agda to be true, so they
should are used sparingly here. They are mainly used in cases where we
know something to be true that is usually needed to prove another
property, but the proof for them is large and non-trivial.

One such example is when dealing with the Barendregt variable and
convention. On paper it is fine to implicitly assume all bound
variables are unique, but Agda is not so lenient. We need to
explicitly perform the alpha conversion and show that the new variable
is distinct from any free variable and that the two expressions are
equivalent
\begin{minted}{agda}
α-conv : ∀ {x e}                                  
       → ∃[ y ] ((∀ {e'} → y ∉ FV e') → (ƛ x ⇒ e ≡ ƛ y ⇒ (e [ x := ` y ])))
\end{minted}
Working with existential quantification and these non-trivial
equivalences can get pretty unwieldly, so in some proofs we simply
just make the postulation
\begin{minted}{agda}
postulate x∉FVv : x ∉ FV v
\end{minted}
